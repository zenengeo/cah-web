plugins {
    id 'org.springframework.boot' version '3.2.4'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'java'
    // https://github.com/qoomon/gradle-git-versioning-plugin
    id 'me.qoomon.git-versioning' version '6.4.3'
    // https://github.com/node-gradle/gradle-node-plugin
    id "com.github.node-gradle.node" version '7.0.2'
    id "io.github.itzg.simple-boot-image" version '3.2.1'
}

version = '0.0.1-SNAPSHOT'
gitVersioning.apply {
    refs {
        tag('.+') {
            version = '${ref}'
        }
        branch('main') {
            version = '${describe.tag}-SNAPSHOT'
        }
        branch('.+') {
            version = '${ref}-SNAPSHOT'
        }
    }
}

group = 'me.itzg'
java {
    sourceCompatibility = JavaVersion.VERSION_17
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

// https://skaffold.dev/docs/pipeline-stages/builders/custom/
test.onlyIf { !System.getenv('SKIP_TESTS') }

repositories {
    mavenCentral()
}

def uiSrcDir = layout.projectDirectory.dir('src/main/ui')
def uiDestBuildDir = layout.buildDirectory.dir('ui')

node {
    download = findProperty('downloadNode') as boolean
    version = '18.12.1'
    nodeProjectDir.set(uiSrcDir)
}

tasks.register('reactTest', NpmTask) {
    dependsOn 'npmInstall'
    args = ['run', 'test']
}

tasks.register('reactBuild', NpmTask) {
    dependsOn 'npmInstall'
    inputs.files(uiSrcDir.file('package.json'), uiSrcDir.file('package-lock.json'))
    inputs.dir(uiSrcDir.dir('src'))
    inputs.dir(uiSrcDir.dir('public'))
    outputs.dir(uiDestBuildDir)

    args = ['run', 'build']

    execOverrides {
        environment('BUILD_PATH', uiDestBuildDir.get().dir('static'))
    }
}

tasks.named('bootJar') {
    dependsOn reactBuild

    classpath(uiDestBuildDir)
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.apache.commons:commons-lang3:3.14.0'
    implementation 'com.github.ben-manes.caffeine:caffeine'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
}

tasks.named('test') {
    useJUnitPlatform()
}
